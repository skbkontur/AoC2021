Перем Высота, Ширина;

Функция ВыигралПоСтрокам(Билет)

	Для Сч1 = 0 По Высота - 1 Цикл

		Выигрыш = Истина;

		Для Сч2 = 0 По Ширина - 1 Цикл

			Если НЕ Билет[Сч1][Сч2] = Неопределено Тогда

				Выигрыш = Ложь;
				Прервать;

			КонецЕсли;

		КонецЦикла;

		Если Выигрыш Тогда

			Возврат Истина;

		КонецЕсли;

	КонецЦикла;

	Возврат Ложь;

КонецФункции

Функция ВыигралПоСтолбцам(Билет)

	Для Сч1 = 0 По Ширина - 1 Цикл

		Выигрыш = Истина;

		Для Сч2 = 0 По Высота - 1 Цикл

			Если НЕ Билет[Сч2][Сч1] = Неопределено Тогда

				Выигрыш = Ложь;
				Прервать;

			КонецЕсли;

		КонецЦикла;

		Если Выигрыш Тогда

			Возврат Истина;

		КонецЕсли;

	КонецЦикла;

	Возврат Ложь;

КонецФункции

Функция ВыигралБилет(Билет, Номер)

	// Сначала вычеркнем номер

	Для Сч1 = 0 По Высота - 1 Цикл

		Для Сч2 = 0 По Ширина - 1 Цикл

			Если Билет[Сч1][Сч2] = Номер Тогда

				Билет[Сч1][Сч2] = Неопределено;

			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

	Возврат ВыигралПоСтрокам(Билет) ИЛИ ВыигралПоСтолбцам(Билет);

КонецФункции

Функция СуммаБилета(Билет)
	Сумма = 0;

	Для Сч1 = 0 По Высота - 1 Цикл

		Для Сч2 = 0 По Ширина - 1 Цикл

			Если Не Билет[Сч1][Сч2] = Неопределено Тогда

				Сумма = Сумма + Число(Билет[Сч1][Сч2]);

			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

	Возврат Сумма;

КонецФункции

Высота = 5;
Ширина = 5;

ЧтениеТекста = Новый ЧтениеТекста("input.txt");

Числа = СтрРазделить(ЧтениеТекста.ПрочитатьСтроку(), ",");

Билеты = Новый Массив;

Строка = ЧтениеТекста.ПрочитатьСтроку();
Пока Строка <> Неопределено Цикл

	Если Строка = "" Тогда

		Билет = Новый Массив;
		Билеты.Добавить(Билет);

	Иначе

		Строка = СокрЛП(СтрЗаменить(Строка, "  ", " "));
		Билет.Добавить(СтрРазделить(Строка, " "));

	КонецЕсли;

	Строка = ЧтениеТекста.ПрочитатьСтроку();

КонецЦикла;

ЧтениеТекста.Закрыть();

ПоследнийБилет = Неопределено;
ПоследнийНомер = Неопределено;
БилетыКУдалению = Новый Массив;

Для Каждого Номер Из Числа Цикл

	Для СчетчикБилетов = 0 По Билеты.Количество() - 1 Цикл

		Билет = Билеты[СчетчикБилетов];
		Если ВыигралБилет(Билет, Номер) Тогда

			ПоследнийБилет = Билет;
			ПоследнийНомер = Номер;
			БилетыКУдалению.Вставить(0, СчетчикБилетов);

		КонецЕсли;

	КонецЦикла;

	Для Каждого КУдалению Из БилетыКУдалению Цикл

		Билеты.Удалить(КУдалению);

	КонецЦикла;
	БилетыКУдалению.Очистить();

КонецЦикла;

Сообщить(СуммаБилета(ПоследнийБилет) * Число(ПоследнийНомер));